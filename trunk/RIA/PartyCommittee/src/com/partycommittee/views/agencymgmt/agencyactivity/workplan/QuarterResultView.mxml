<?xml version="1.0" encoding="utf-8"?>
<control:EscapeWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
					  xmlns:s="library://ns.adobe.com/flex/spark" 
					  xmlns:mx="library://ns.adobe.com/flex/mx" 
					  xmlns:control="com.partycommittee.control.*"
					  horizontalAlign="center" verticalAlign="middle"
					  showCloseButton="true" titleStyleName="panelTitle"
					  close="PopupMgr.instance.removeWindow(this)" 
					  height="650" width="800"
					  title="季度执行情况"
					  creationComplete="onCreationCompleted()"
					  xmlns:button="com.partycommittee.control.button.*" 
					  xmlns:richtexttoolbar="com.partycommittee.control.richtexttoolbar.*">
	<fx:Script>
		<![CDATA[
			import com.partycommittee.model.ModelLocator;
			import com.lnczx.utils.DateUtilExt;
			import com.partycommittee.events.PcRemindLockEvent;
			import com.partycommittee.events.PcWorkPlanEvent;
			import com.partycommittee.manager.popup.PopupMgr;
			import com.partycommittee.util.CRUDEventType;
			import com.partycommittee.util.DataproviderFactory;
			import com.partycommittee.util.DateUtil;
			import com.partycommittee.util.WorkPlanCodeUtil;
			import com.partycommittee.util.WorkPlanStatusUtil;
			import com.partycommittee.vo.PcAgencyVo;
			import com.partycommittee.vo.PcRemindConfigVo;
			import com.partycommittee.vo.PcRemindLockVo;
			import com.partycommittee.vo.PcWorkPlanContentVo;
			import com.partycommittee.vo.PcWorkPlanVo;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			
			[Bindable]
			private var workPlanStatusLbl:String;
			
			private var isNew:Boolean = true;
			
			[Bindable]
			public var detailMode:Boolean = false;
			
			[Bindable]
			private var year:Number;
			[Bindable]
			private var quarter:Number = 1;
			
			private var _workPlan:PcWorkPlanVo;
			[Bindable]
			public function get workPlan():PcWorkPlanVo {
				return this._workPlan;
			}
			public function set workPlan(value:PcWorkPlanVo):void {
				this._workPlan = value;
			}
			
			[Bindable]
			private var yearDp:ArrayCollection;
			[Bindable]
			private var quarterDp:ArrayCollection = new ArrayCollection([
				{label:"第一季度", value:1},
				{label:"第二季度", value:2},
				{label:"第三季度", value:3},
				{label:"第四季度", value:4}
			]);
			
			private var _agency:PcAgencyVo;
			[Bindable]
			public function get agency():PcAgencyVo {
				return this._agency;
			}
			public function set agency(value:PcAgencyVo):void {
				this._agency = value;
			}
			
			private var _parentView:QuarterWorkPlanListView;
			[Bindable]
			public function get parentView():QuarterWorkPlanListView {
				return this._parentView;
			}
			public function set parentView(value:QuarterWorkPlanListView):void {
				this._parentView = value;
			}
			
			private function onCreationCompleted():void {
				updateData();
			}
			
			private function updateData():void {
				if (!agency || !workPlan) {
					initRemindLock();
					return;
				}
				
				year = workPlan.year;
				quarter = workPlan.quarter;
				
				if (workPlan.id) {
					isNew = false;
					workPlanStatusLbl = WorkPlanStatusUtil.getWorkPlanStatusDes(workPlan.statusId);
					if (workPlanStatusLbl != "") {
						workPlanStatusLbl = " (" + workPlanStatusLbl + ")";
					}
					
					if (workPlan.statusId == 0 || workPlan.statusId == 1) {
						QuearterViewStack.selectedChild = reportForm;
					
						if (workPlan.workPlanContent) {
							personTxt.text = "填报人:" + workPlan.workPlanContent.memberName;
							textArea.htmlText = workPlan.workPlanContent.content;
						} else {
							loadWorkPlanContent();
						}
						initRemindLock();
					} else {
						QuearterViewStack.selectedChild = browseForm;
						if (workPlan.workPlanContent) {
							b_personTxt.text = "填报人:" + workPlan.workPlanContent.memberName;
							b_textArea.htmlText = workPlan.workPlanContent.content;
							if (workPlan.statusId == 4) {
								getWorkPlanComment(workPlan);
							}
						} else {
							loadWorkPlanContent();
						}
					}
				} else {
					QuearterViewStack.selectedChild = reportForm;
					isNew = true;
					initRemindLock();
				}
			}
			
			private function loadWorkPlanContent():void {
				var evt:PcWorkPlanEvent = new PcWorkPlanEvent(PcWorkPlanEvent.GET_WORKPLAN_CONTENT_BY_WORKPLANID);
				evt.workPlan = workPlan;
				evt.successCallback = onLoadContentSuccess;
				evt.failureCallback = onLoadContentFailure;
				this.enabled = false;
				evt.dispatch();
			}
			
			private function onLoadContentSuccess(data:Object, evt:PcWorkPlanEvent):void {
				this.enabled = true;
				workPlan.workPlanContent = data as PcWorkPlanContentVo;
				if (workPlan.statusId == 0 || workPlan.statusId == 1) {
					personTxt.text = workPlan.workPlanContent.memberName;
					textArea.htmlText = workPlan.workPlanContent.content;
				} else {
					b_personTxt.text = "填报人:" + workPlan.workPlanContent.memberName;
					b_textArea.htmlText = workPlan.workPlanContent.content;		
					if (workPlan.statusId == 4) {
						getWorkPlanComment(workPlan);
					}					
				}
			}
			
			private function onGetWorkplanCommentFailure(info:Object, evt:PcWorkPlanEvent):void {
				Alert.show("获取内容失败！");
			}					
			
			private function getWorkPlanComment(workPlan:PcWorkPlanVo):void {
				var workPlanEvt:PcWorkPlanEvent = new PcWorkPlanEvent(PcWorkPlanEvent.GET_WORKPLAN_COMMENT);
				workPlanEvt.workPlan = workPlan;
				workPlanEvt.successCallback = onGetWorkplanCommentSuccess;
				workPlanEvt.failureCallback = onGetWorkplanCommentFailure;
				workPlanEvt.dispatch();
			}
			
			private function onGetWorkplanCommentSuccess(data:Object, evt:PcWorkPlanEvent):void {
				this.enabled = true;
				var workplanContent:PcWorkPlanContentVo = data as PcWorkPlanContentVo;
				if (!workplanContent) {
					return;
				}
				browseForm_evaluate.percentWidth = 100;
				browseForm_evaluate.percentHeight = 30;
				browseForm_evaluate.visible = "true";
				b_textArea_eva.htmlText = workplanContent.content;
				b_personTxt_eva.text = "评价人:" + workplanContent.memberName;
				dateTxt.text = "评价日期:" + DateUtil.toISOString(workplanContent.updatetime);
			}			
			
			private function onLoadContentFailure(info:Object, evt:PcWorkPlanEvent):void {
				this.enabled = true;
				Alert.show("读取执行情况内容失败！");
			}
			
			private function resetData():void {
				workPlan = null;
				personTxt.text = "";
				textArea.htmlText = "";
			}
			
			private function onSaveAndCommit():void {
				if (!isNew) {
					doUpdate(true);
				} else {
					doCreate(true);
				}
			}
			
			private function onSaveWithoutSubmit():void {
				if (!isNew) {
					doUpdate();
				} else {
					doCreate();
				}
			}
			
			private function doUpdate(isCommit:Boolean = false):void {
				var workPlanEvt:PcWorkPlanEvent = new PcWorkPlanEvent(CRUDEventType.UPDATE);
				var updateWorkPlan:PcWorkPlanVo = getUpdateWorkPlan();
				updateWorkPlan.statusId = isCommit ? 2 : 1;
				workPlanEvt.workPlan = updateWorkPlan;
				workPlanEvt.successCallback = onWorkPlanUpdateSuccess;
				workPlanEvt.failureCallback = onWorkPlanUpdateFailure;
				this.enabled = false;
				workPlanEvt.dispatch();
			}
			
			private function doCreate(isCommit:Boolean = false):void {
				var workPlanEvt:PcWorkPlanEvent = new PcWorkPlanEvent(CRUDEventType.CREATE);
				var newWorkPlan:PcWorkPlanVo = getNewWorkPlan();
				newWorkPlan.statusId = isCommit ? 2 : 1;
				workPlanEvt.workPlan = newWorkPlan;
				workPlanEvt.successCallback = onWorkPlanCreateSuccess;
				workPlanEvt.failureCallback = onWorkPlanCreateFailure;
				this.enabled = false;
				workPlanEvt.dispatch();
			}
			
			private function onWorkPlanCreateSuccess(data:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				parentView.updateData();
				this.close();
			}
			
			private function onWorkPlanCreateFailure(info:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				Alert.show("保存季度执行情况失败！");
			}
			
			private function onWorkPlanUpdateSuccess(data:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				parentView.updateData();
				this.close();
			}
			
			private function onWorkPlanUpdateFailure(info:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				Alert.show("修改季度执行情况失败！");
			}
			
			private function getNewWorkPlan():PcWorkPlanVo {
				var workPlan:PcWorkPlanVo = new PcWorkPlanVo();
				workPlan.typeId = PCConst.WORKPLAN_TYPE_QUARTER_SUMMARY;
				workPlan.year = year;
				workPlan.quarter = quarter;
				workPlan.agencyId = agency.id;
				workPlan.workPlanContent = getWorkPlanContent();
				return workPlan;
			}
			
			private function getUpdateWorkPlan():PcWorkPlanVo {
				workPlan.workPlanContent.memberName = personTxt.text;
				workPlan.workPlanContent.content = textArea.htmlText;
				return workPlan;
			}
			
			private function getWorkPlanContent():PcWorkPlanContentVo {
				var workPlanContent:PcWorkPlanContentVo = new PcWorkPlanContentVo();
				workPlanContent.content = textArea.htmlText;
				workPlanContent.memberName = personTxt.text;
				workPlanContent.type = PCConst.WORKFLOW_TYPE_REPORT;
				return workPlanContent;
			}
			
			private function onBrowseEvaluate():void {
				var commentView:CommentView = new CommentView();
				commentView.workPlan = workPlan;
				PopupMgr.instance.popupWindow(commentView);
			}
			
			private function initRemindLock():void {
				var remindLockEvt:PcRemindLockEvent = new PcRemindLockEvent(PcRemindLockEvent.GET_REMIND_LOCK_BY_ID);
				remindLockEvt.agencyId = agency.id;
				remindLockEvt.year = year;
				remindLockEvt.quarter = quarter;
				remindLockEvt.month = 0;
				remindLockEvt.typeId = 3;
				remindLockEvt.successCallback = getRemindLockSuccess;
				remindLockEvt.failureCallback = getRemindLockFailure
				remindLockEvt.dispatch();				
			}
			
			private function setRemindLock():void {
				var vo:PcRemindConfigVo = getRemindVo(3);
				var y:Number = new Date().getFullYear();
				var cur_date = new Date();
				
				if (vo) {
					var endDate:Date = DateUtilExt.endOfQuarter(year, quarter);
					endDate = DateUtilExt.nextSomeDay(endDate, vo.endDay);
					if (DateUtilExt.after(cur_date, endDate)) {
						btn_commit.label ="已锁定";
						btn_commit.enabled = false;
					} else {
						btn_commit.label ="上报";
						btn_commit.enabled = true;
					}
				}
			}	
			
			private function getRemindLockSuccess(data:Object, evt:PcRemindLockEvent):void {
				if (data) {
					var vo:PcRemindLockVo = data as PcRemindLockVo;
					if (vo.statusId == 9) {
						setRemindLock();
					}
				}
			}
			
			private function getRemindLockFailure(data:Object, evt:PcRemindLockEvent):void {
				if (btn_commit.label == "已锁定") {
					btn_commit.label ="上报";
					btn_commit.enabled = true;
				}
			}		
			
			public function getRemindVo(typeId:Number):PcRemindConfigVo {
				var model:ModelLocator = ModelLocator.getInstance();
				var dd:ArrayCollection = model.remindConfigCollection;
				for(var i = 0; i < dd.length; i++ ) {
					if ( (dd.getItemAt(i) as PcRemindConfigVo).typeId == typeId) {
						return dd.getItemAt(i) as PcRemindConfigVo
					}
				}
				return null;
			}								
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<mx:VBox width="100%" height="100%">
		<mx:HBox width="100%" horizontalAlign="center"
				 horizontalScrollPolicy="off" verticalScrollPolicy="off">
			<mx:Label fontSize="20" fontWeight="bold" text="{year + '年度第' + quarter + '季度执行情况'}"/>
			<mx:Label fontSize="20" fontWeight="bold"
					  text="{workPlanStatusLbl}"/>
		</mx:HBox>
		<mx:HBox width="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
			<mx:Label text="年    份："/>
			<mx:Label text="{year}"/>
		</mx:HBox>
		<mx:HBox width="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
			<mx:Label text="季    度："/>
			<mx:Label text="第{quarter.toString()}季度"/>
		</mx:HBox>		
		<mx:ViewStack id="QuearterViewStack" width="100%" height="100%" creationPolicy="all"
					  left="6" right="6" top="6" bottom="6">
			<mx:VBox id="reportForm" width="100%" height="100%"
					 left="6" right="6" top="6" bottom="6"
					 horizontalScrollPolicy="off" verticalScrollPolicy="off">
		
		
				<richtexttoolbar:PopupRichTextToolBar width="100%" target="{textArea}"/>
				<mx:TextArea id="textArea" width="100%" height="100%"
							 editable="{!workPlan || workPlan.statusId == 1}"/>
				<mx:HBox width="100%" horizontalAlign="left" horizontalScrollPolicy="off" verticalScrollPolicy="off">
					<mx:Label text="填写人："/>
					<mx:TextInput id="personTxt" width="200"
								  editable="{!workPlan || workPlan.statusId == 1}"/>
				</mx:HBox>		
				<mx:ControlBar width="100%" horizontalAlign="center" verticalAlign="middle">
					<button:PcButton label="暂存" click="onSaveWithoutSubmit()"
									 visible="{!workPlan || workPlan.statusId == 1}"
									 includeInLayout="{!detailMode}"/>
					<button:PcButton label="上报" id="btn_commit" click="onSaveAndCommit()"
									 visible="{!workPlan || workPlan.statusId == 1}"
									 includeInLayout="{!detailMode}"/>
					<button:PcButton label="关闭" click="close()"/>
				</mx:ControlBar>
			</mx:VBox>
			
			<mx:VBox id="browseForm" width="100%" height="100%"
					 left="6" right="6" top="6" bottom="6"
					 horizontalScrollPolicy="off" verticalScrollPolicy="off">
				<mx:HBox width="100%" horizontalAlign="left" height="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
					<mx:VBox left="6" right="6" top="6" bottom="6">
						<mx:Label text="工"     />
						<mx:Label text="作"     />
						<mx:Label text="执"     />
						<mx:Label text="行"     />
						<mx:Label text="情"     />
						<mx:Label text="况"     />
					</mx:VBox>						
					<mx:VBox width="100%" height="100%">
						<mx:TextArea id="b_textArea" width="100%" height="100%"
								 editable="false"/>
						<mx:Label id="b_personTxt" text="填写人："/>	
					</mx:VBox>
				</mx:HBox>
				
				<mx:HBox id="browseForm_evaluate"  horizontalAlign="left" width="0" height="0" horizontalScrollPolicy="off" verticalScrollPolicy="off" visible="false">
					<mx:VBox left="6" right="6" top="6" bottom="6">
						<mx:Label text="评"     />
						<mx:Label text="价"     />
					</mx:VBox>	
					<mx:VBox width="100%" height="100%">
						<mx:TextArea id="b_textArea_eva" width="100%" height="100%"
									 editable="false"/>
						<mx:HBox  horizontalCenter="0" width="100%">
							<mx:Label left="0"   id="dateTxt"/>
							<mx:Spacer width="100%"/>
							<mx:Label right="0" id="b_personTxt_eva"/>
						</mx:HBox>							
					</mx:VBox>
				</mx:HBox>
				
				<mx:ControlBar width="100%" horizontalAlign="center" verticalAlign="middle">
					<button:PcButton label="关闭" click="close()"/>
				</mx:ControlBar>
			</mx:VBox>
		</mx:ViewStack>
	</mx:VBox>
</control:EscapeWindow>
