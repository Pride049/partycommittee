<?xml version="1.0" encoding="utf-8"?>
<control:EscapeWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
					  xmlns:s="library://ns.adobe.com/flex/spark" 
					  xmlns:mx="library://ns.adobe.com/flex/mx" 
					  xmlns:control="com.partycommittee.control.*"
					  horizontalAlign="center" verticalAlign="middle"
					  showCloseButton="true" titleStyleName="panelTitle"
					  close="PopupMgr.instance.removeWindow(this)" 
					  xmlns:button="com.partycommittee.control.button.*"
					  horizontalScrollPolicy="off" verticalScrollPolicy="off"
					  creationComplete="onCreationCompleted()" 
					  xmlns:tree="com.partycommittee.control.tree.*"
					  xmlns:component="com.partycommittee.views.agencymgmt.component.*">
	<fx:Script>
		<![CDATA[
			import com.partycommittee.control.tree.classes.Node;
			import com.partycommittee.control.tree.classes.TreeOperationEvent;
			import com.partycommittee.events.PcAgencyEvent;
			import com.partycommittee.manager.popup.PopupMgr;
			import com.partycommittee.manager.tree.TreeMgr;
			import com.partycommittee.model.ModelLocator;
			import com.partycommittee.util.AgencyCodeUtil;
			import com.partycommittee.util.CRUDEventType;
			import com.partycommittee.vo.PcAgencyVo;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import mx.events.PropertyChangeEvent;
			import mx.managers.PopUpManager;
			
			[Bindable]
			public var type:String = CRUDEventType.CREATE;
			
			[Bindable]
			private var codeDescription:String;
			
			private var _agencyCodeId:Number;
			[Bindable]
			public function get agencyCodeId():Number {
				return this._agencyCodeId;
			}
			public function set agencyCodeId(value:Number):void {
				this._agencyCodeId = value;
				codeDescription = AgencyCodeUtil.getAgencyCodeDes(this._agencyCodeId);
			}
			
			private var _parentAgency:PcAgencyVo;
			[Bindable]
			public function get parentAgency():PcAgencyVo {
				return this._parentAgency;
			}
			public function set parentAgency(value:PcAgencyVo):void {
				this._parentAgency = value;
			}
			
			private var _agency:PcAgencyVo;
			[Bindable]
			public function get agency():PcAgencyVo {
				return this._agency;
			}
			public function set agency(value:PcAgencyVo):void {
				this._agency = value;
			}
						
			[Bindable]
			private var model:ModelLocator = ModelLocator.getInstance();
			
			[Bindable]
			private var dp:ArrayCollection;			
			
			private function onCreationCompleted():void {
				if (this._agency) {
					updateData();
				}
			}
			
			private function updateData():void {
				if (!this._agency) {
					return;
				}
				
				codeDescription = AgencyCodeUtil.getAgencyCodeDes(agency.codeId);
				nameTxt.text = agency.name;

				
				var agencyEvent:PcAgencyEvent = new PcAgencyEvent(PcAgencyEvent.GET_ROOT_AGENCY_FOR_PRIVILEGE);
				agencyEvent.userId = 1;
				agencyEvent.successCallback = onGetRootAgency;
				agencyEvent.dispatch();				
				
			}
			
			private function onGetRootAgency(data:Object, evt:PcAgencyEvent):void {
				dp = new ArrayCollection();
				var rootItem:Node = TreeMgr.getInstance().createItem(data as PcAgencyVo);
				dp.addItem(rootItem);
			}			
			
			private function save():void {
				var node:Node = this.privilegeTree.selectedItem as Node;
				
				if (!node) {
					Alert.show("请选择划转目标!");
					return;
				}
				var agencyVo:PcAgencyVo = node.entity as PcAgencyVo;
				if (!agencyVo) {
					return;
				}
				
				if (agencyVo.id == parentAgency.id) {
					Alert.show("当前选择支部与之前支部相同!");
					return;
				}
				
				Alert.show("确定将要划转到【" + agencyVo.name + "】？", "提示", 
					Alert.YES | Alert.NO, FlexGlobals.topLevelApplication.root, onMoveAlertClose);
				
			}
			
			private function onMoveAlertClose(event:CloseEvent):void {
				if (event.detail == Alert.YES) {
					doUpdate();
				}
			}			
			
			private function doUpdate():void {
				
				var node:Node = this.privilegeTree.selectedItem as Node;

				var agencyVo:PcAgencyVo = node.entity as PcAgencyVo;
				if (!agencyVo) {
					return;
				}				
				
				
				var updateAgency:PcAgencyVo = new PcAgencyVo();
				
				updateAgency.parentId = agencyVo.id;
				updateAgency.id = agency.id;				
				updateAgency.name = agency.name;
				updateAgency.code = agency.code;
				updateAgency.codeId = agency.codeId;
				updateAgency.setupDatetime = agency.setupDatetime;
				updateAgency.pcount = agency.pcount;
				updateAgency.zbnum = agency.zbnum;
				
				if (updateAgency) {
					var evt:PcAgencyEvent = new PcAgencyEvent(CRUDEventType.MOVE, updateAgency);
					evt.successCallback = onCallbackSuccessed;
					evt.failureCallback = onCallbackFailure;
					evt.dispatch();
				}
				this.enabled = false;
			}
			
			private function onCallbackSuccessed(data:Object, evt:PcAgencyEvent):void {
				this.enabled = true;
				// Dispatch event to update tree.
				model.tree.dispatchEvent(new TreeOperationEvent(
				TreeOperationEvent.NODE_CHANGED, model.tree.selectedItem));

				close();
			}
						
			private function onCallbackFailure(info:Object, evt:PcAgencyEvent):void {
				this.enabled = true;
				Alert.show(info.toString());
			}
			
			private function getUpdateAgency():PcAgencyVo {
				if (!agency) {
					return null;
				}
				var agencyVo:PcAgencyVo = new PcAgencyVo();
				agencyVo.parentId = agency.parentId;
				agencyVo.id = agency.id;
				agencyVo.codeId = agency.codeId;
				agencyVo.code = agency.code;
				agencyVo.name = nameTxt.text;

				agencyVo.setupDatetime = agency.setupDatetime;
				return agencyVo;
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<mx:VBox height="100%" width="100%"
			 horizontalAlign="center" verticalAlign="middle"
			 horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<mx:Form width="450" height="100%"
				 minWidth="200">
			<mx:FormItem label="组织性质:" width="100%">
				<mx:Label width="100%" text="{codeDescription}"/>
			</mx:FormItem>
			<mx:FormItem label="组织名称:" required="true" width="100%">
				<mx:Label id="nameTxt" width="100%"/>
			</mx:FormItem>
		</mx:Form>
		<mx:Label text="划转到:"/>
		<tree:LoadingParentTree id="privilegeTree" width="100%" height="100%" dataProvider="{dp}"
						  />
		<mx:ControlBar width="100%" horizontalAlign="center">
			<button:PcButton label="保存" click="save()"/>
			<button:PcButton label="取消" click="close()"/>
		</mx:ControlBar>
	</mx:VBox>
</control:EscapeWindow>
