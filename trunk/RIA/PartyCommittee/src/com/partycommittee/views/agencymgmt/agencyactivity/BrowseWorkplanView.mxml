<?xml version="1.0" encoding="utf-8"?>
<navigation:NavigationView xmlns:fx="http://ns.adobe.com/mxml/2009" 
						   xmlns:s="library://ns.adobe.com/flex/spark" 
						   xmlns:mx="library://ns.adobe.com/flex/mx"
						   styleName="mainCanvas"
						   width="100%" height="100%" 
						   xmlns:navigation="com.partycommittee.manager.navigation.*" 
						   xmlns:component="com.partycommittee.views.agencymgmt.component.*" 
						   xmlns:button="com.partycommittee.control.button.*"
						   creationComplete="enterView()">
	<fx:Script>
		<![CDATA[
			import com.partycommittee.events.PcAgencyEvent;
			import com.partycommittee.events.PcWorkPlanEvent;
			import com.partycommittee.manager.popup.PopupMgr;
			import com.partycommittee.model.ModelLocator;
			import com.partycommittee.util.WorkPlanCodeUtil;
			import com.partycommittee.views.agencymgmt.agencyactivity.workplan.QuarterWorkPlanView;
			import com.partycommittee.views.agencymgmt.agencyactivity.workplan.WorkPlanHandlerView;
			import com.partycommittee.views.agencymgmt.agencyactivity.workplan.YearWorkPlanView;
			import com.partycommittee.vo.PcAgencyVo;
			import com.partycommittee.vo.PcWorkPlanVo;
			
			import mx.collections.ArrayCollection;
			import mx.containers.Canvas;
			import mx.controls.Alert;
			import mx.events.PropertyChangeEvent;
			
			[Bindable]
			private var model:ModelLocator = ModelLocator.getInstance();
			
			private var _agency:PcAgencyVo;
			[Bindable]
			public function get agency():PcAgencyVo {
				return this._agency;
			}
			public function set agency(value:PcAgencyVo):void {
				this._agency = value;
			}
			
			[Bindable]
			private var dp:ArrayCollection = new ArrayCollection();
			
			private var year:Number;
			
			public function enterView():void {
				onNavigateIn();
				this.year = model.reportYear;
			}
			
			override protected function onNavigateIn():void {
				if (agency != model.focusAgencyVo) {
					agency = model.focusAgencyVo;
				}
				this.year = model.reportYear;
				refreshData();
				model.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, onModelChanged);
			}
			
			override protected function onNavigateOut():void {
				model.removeEventListener(PropertyChangeEvent.PROPERTY_CHANGE, onModelChanged);
			}
			
			private function onModelChanged(event:PropertyChangeEvent):void {
				if (event.property == "focusAgencyVo" || event.property == 'reportYear') {
					agency = model.focusAgencyVo;
					this.year = model.reportYear;
					refreshData();
				}
			}
			
			public function refreshData():void {
				if (!agency) {
					return;
				}
				if (agency.children && agency.children.length) {
					getCommitWorkPlan();
					return;
				}
				var pcAgencyEvt:PcAgencyEvent = new PcAgencyEvent(PcAgencyEvent.GET_CHILDREN, agency);
				pcAgencyEvt.successCallback = onGetChildrenSuccess;
				pcAgencyEvt.failureCallback = onGetChildrenFailure;
				pcAgencyEvt.dispatch();
			}
			
			private function onGetChildrenSuccess(data:Object, evt:PcAgencyEvent):void {
				agency.children = data as ArrayCollection;
				if (agency.children.length > 0) {
					getCommitWorkPlan();
				}
			}
			
			private function onGetChildrenFailure(data:Object, evt:PcAgencyEvent):void {
				Alert.show(data.toString());
			}
			
			private function getCommitWorkPlan():void {
				var pcWorkPlanEvent:PcWorkPlanEvent = new PcWorkPlanEvent(PcWorkPlanEvent.GET_COMMIT_CHILDREN_WORKPLAN);
				pcWorkPlanEvent.agency = agency;
				
				if (this.year == 0) this.year = new Date().getFullYear();
				pcWorkPlanEvent.year = this.year;
				pcWorkPlanEvent.failureCallback = getWorkPlanFailure;
				pcWorkPlanEvent.successCallback = getWorkPlanSuccess;
				pcWorkPlanEvent.dispatch();
			}
			
			private function getWorkPlanSuccess(data:Object, evt:PcWorkPlanEvent):void {
				dp = data as ArrayCollection;
			}
			
			private function getWorkPlanFailure(info:Object, evt:PcWorkPlanEvent):void {
				Alert.show(info.toString());
			}
			
			private function typeLblFunc(item:Object, col:*):String {
				return WorkPlanCodeUtil.getWorkPlanTypeDes((item as PcWorkPlanVo).typeId);
			}
			
			private function agencyLblFunc(item:Object, col:*):String {
				var agencyId:Number = (item as PcWorkPlanVo).agencyId;
				for each (var child:PcAgencyVo in agency.children) {
					if (child.id == agencyId) {
						return child.name;
					}
				}
				return "";
			}
			
			private function quarterLblFunc(item:Object, col:*):String {
				switch ((item as PcWorkPlanVo).quarter) {
					case 1:
						return "第一季度";
					case 2:
						return "第二季度";
					case 3:
						return "第三季度";
					case 4:
						return "第四季度";
				}
				return "";
			}
			
			public function evaluateWorkPlan(workPlan:PcWorkPlanVo):void {
				var win:WorkPlanHandlerView = new WorkPlanHandlerView();
				win.agency = agency;
				win.workPlan = workPlan;
				win.parentView = this;
				win.type = PCConst.WORKFLOW_TYPE_EVALUATE;
				PopupMgr.instance.popupWindow(win);
			}
			
			private function onItemDoubleClick():void {
				
				var vo:PcWorkPlanVo = datagrid.selectedItem as PcWorkPlanVo;
				if (vo) {
					evaluateWorkPlan(vo);
				}
				
			}				
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<mx:VBox width="100%" height="100%">

		<mx:AdvancedDataGrid id="datagrid" width="100%" height="100%" 
							 headerStyleName="dataGridHeader"
							 dataProvider="{dp}"
							 doubleClickEnabled="true"
							 doubleClick="onItemDoubleClick()">
			<mx:columns>
				<!--<mx:AdvancedDataGridColumn headerText="序号" dataField="id"/>-->
				<mx:AdvancedDataGridColumn headerText="类型" dataField="typeId"
										   labelFunction="typeLblFunc"/>
				<mx:AdvancedDataGridColumn headerText="党组织" dataField="agencyId"
										   labelFunction="agencyLblFunc"/>
				<mx:AdvancedDataGridColumn headerText="年度" dataField="year"/>
				<mx:AdvancedDataGridColumn headerText="季度" dataField="quarter"
										   labelFunction="quarterLblFunc"/>
				<mx:AdvancedDataGridColumn headerText="操作">
					<mx:itemRenderer>
						<fx:Component>
							<mx:Box horizontalAlign="center" verticalAlign="middle"
									horizontalScrollPolicy="off" verticalScrollPolicy="off">
								<fx:Script>
									<![CDATA[
										import com.partycommittee.util.LblFunctionHelper;
										import com.partycommittee.vo.PcWorkPlanVo;

										protected function actionClick():void {
											outerDocument.evaluateWorkPlan(data as PcWorkPlanVo);
										}
										
										protected function formatActionButtonLabel(item:Object):String {											
											if (item && (item as PcWorkPlanVo).statusId >= 4  ) {
												return '查看';
											}
											return '评价';
										}
										
																				
									]]>
								</fx:Script>
								<button:PcButton id="editBtn" label="{formatActionButtonLabel(data as PcWorkPlanVo)}" click="actionClick()"/>							
							</mx:Box>
						</fx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>
			</mx:columns>
		</mx:AdvancedDataGrid>
	</mx:VBox>
	
</navigation:NavigationView>
