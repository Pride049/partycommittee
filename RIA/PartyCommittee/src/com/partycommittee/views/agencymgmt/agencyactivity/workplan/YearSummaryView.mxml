<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx"
		   styleName="mainCanvas"
		   width="100%" height="100%" 
		   label="年终总结"
		   creationComplete="onCreationCompleted()"
		   xmlns:button="com.partycommittee.control.button.*" 
		   xmlns:richtexttoolbar="com.partycommittee.control.richtexttoolbar.*">
	<fx:Script>
		<![CDATA[
			import com.partycommittee.events.PcWorkPlanEvent;
			import com.partycommittee.util.CRUDEventType;
			import com.partycommittee.util.DataproviderFactory;
			import com.partycommittee.util.WorkPlanCodeUtil;
			import com.partycommittee.vo.PcAgencyVo;
			import com.partycommittee.vo.PcWorkPlanContentVo;
			import com.partycommittee.vo.PcWorkPlanVo;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
			[Bindable]
			private var year:Number;
			
			private var _workPlan:PcWorkPlanVo;
			[Bindable]
			public function get workPlan():PcWorkPlanVo {
				return this._workPlan;
			}
			public function set workPlan(value:PcWorkPlanVo):void {
				this._workPlan = value;
			}
			
			[Bindable]
			private var yearDp:ArrayCollection;
			
			private var _agency:PcAgencyVo;
			[Bindable]
			public function get agency():PcAgencyVo {
				return this._agency;
			}
			public function set agency(value:PcAgencyVo):void {
				this._agency = value;
			}
			
			private function onCreationCompleted():void {
				addEventListener(FlexEvent.SHOW, onViewShow);
			}
			
			private function onViewShow(event:FlexEvent):void {
				if (agency) {
					updateData();
				}
			}
			
			private function updateData():void {
				if (!agency) {
					return;
				}
				
				yearDp = DataproviderFactory.getYearDp();
				if (yearDp && yearDp.length > 0) {
					yearCmb.selectedIndex = 0;
					onYearCmbChanged();
				}
			}
			
			private function resetData():void {
				workPlan = null;
				personTxt.text = "";
				textArea.htmlText = "";
			}
			
			private function onYearCmbChanged():void {
				this.year = this.yearCmb.selectedItem as Number;
				var workPlanEvt:PcWorkPlanEvent = new PcWorkPlanEvent(PcWorkPlanEvent.GET_WORKPLAN_YEARLY_SUMMARY, agency);
				workPlanEvt.year = year;
				workPlanEvt.successCallback = onGetWorkPlanSuccess;
				workPlanEvt.failureCallback = onGetWorkPlanFailure;
				this.enabled = false;
				workPlanEvt.dispatch();
			}
			
			private function onGetWorkPlanSuccess(data:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				if (!data) {
					resetData();
					return;
				}
				workPlan = data as PcWorkPlanVo;
				personTxt.text = workPlan.workPlanContent.memberName;
				textArea.htmlText = workPlan.workPlanContent.content;
			}
			
			private function onGetWorkPlanFailure():void {
				this.enabled = true;
			}
			
			private function onSaveAndCommit():void {
				if (workPlan) {
					doUpdate(true);
				} else {
					doCreate(true);
				}
			}
			
			private function onSaveWithoutSubmit():void {
				if (workPlan) {
					doUpdate();
				} else {
					doCreate();
				}
			}
			
			private function doUpdate(isCommit:Boolean = false):void {
				var workPlanEvt:PcWorkPlanEvent = new PcWorkPlanEvent(CRUDEventType.UPDATE);
				var updateWorkPlan:PcWorkPlanVo = getUpdateWorkPlan();
				updateWorkPlan.statusId = isCommit ? 1 : 2;
				workPlanEvt.workPlan = updateWorkPlan;
				workPlanEvt.successCallback = onWorkPlanUpdateSuccess;
				workPlanEvt.failureCallback = onWorkPlanUpdateFailure;
				this.enabled = false;
				workPlanEvt.dispatch();
			}
			
			private function doCreate(isCommit:Boolean = false):void {
				var workPlanEvt:PcWorkPlanEvent = new PcWorkPlanEvent(CRUDEventType.CREATE);
				var newWorkPlan:PcWorkPlanVo = getNewWorkPlan();
				newWorkPlan.statusId = isCommit ? 1 : 2;
				workPlanEvt.workPlan = newWorkPlan;
				workPlanEvt.successCallback = onWorkPlanCreateSuccess;
				workPlanEvt.failureCallback = onWorkPlanCreateFailure;
				this.enabled = false;
				workPlanEvt.dispatch();
			}
			
			private function onWorkPlanCreateSuccess(data:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				onYearCmbChanged();
			}
			
			private function onWorkPlanCreateFailure(info:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				Alert.show("保存年终工作总结失败！");
			}
			
			private function onWorkPlanUpdateSuccess(data:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				onYearCmbChanged();
			}
			
			private function onWorkPlanUpdateFailure(info:Object, workPlanEvt:PcWorkPlanEvent):void {
				this.enabled = true;
				Alert.show("修改年终工作总结失败！");
			}
			
			private function getNewWorkPlan():PcWorkPlanVo {
				var workPlan:PcWorkPlanVo = new PcWorkPlanVo();
				workPlan.typeId = PCConst.WORKPLAN_TYPE_YEARLY_SUMMARY;
				workPlan.year = year;
				workPlan.agencyId = agency.id;
				workPlan.workPlanContent = getWorkPlanContent();
				return workPlan;
			}
			
			private function getUpdateWorkPlan():PcWorkPlanVo {
				workPlan.workPlanContent.memberName = personTxt.text;
				workPlan.workPlanContent.content = textArea.htmlText;
				return workPlan;
			}
			
			private function getWorkPlanContent():PcWorkPlanContentVo {
				var workPlanContent:PcWorkPlanContentVo = new PcWorkPlanContentVo();
				workPlanContent.content = textArea.htmlText;
				workPlanContent.memberName = personTxt.text;
				workPlanContent.type = PCConst.WORKFLOW_TYPE_REPORT;
				return workPlanContent;
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<mx:VBox width="100%" height="100%"
			 left="6" right="6" top="6" bottom="6"
			 horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<mx:HBox width="100%" horizontalAlign="center"
				 horizontalScrollPolicy="off" verticalScrollPolicy="off">
			<mx:Label fontSize="20" fontWeight="bold" text="{year + '年终工作总结'}"/>
			<mx:Label fontSize="20" fontWeight="bold"
					  text="{(workPlan &amp;&amp; workPlan.statusId == 1) ? ' （已上报）' : ''}"/>
		</mx:HBox>
		<mx:HBox width="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
			<mx:Label text="填写人："/>
			<mx:TextInput id="personTxt" width="200"
						  editable="{!workPlan || workPlan.statusId == 2}"/>
		</mx:HBox>
		<richtexttoolbar:PopupRichTextToolBar width="100%" target="{textArea}"/>
		<mx:TextArea id="textArea" width="100%" height="100%"
					 editable="{!workPlan || workPlan.statusId == 2}"/>
		<mx:HBox width="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off">
			<mx:Label text="年    份："/>
			<mx:ComboBox id="yearCmb" dataProvider="{yearDp}" change="onYearCmbChanged()"/>
		</mx:HBox>
		<mx:ControlBar width="100%" horizontalAlign="center" verticalAlign="middle">
			<button:PcButton label="暂存" click="onSaveWithoutSubmit()"
							 enabled="{!workPlan || workPlan.statusId == 2}"/>
			<button:PcButton label="上报" click="onSaveAndCommit()"
							 enabled="{!workPlan || workPlan.statusId != 1}"/>
		</mx:ControlBar>
	</mx:VBox>
	
</mx:Canvas>
